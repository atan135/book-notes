## 材质

在现实世界中，每个物体对光会产生不同的反应，我们为每个物体定义一个 **材质（Material）** 的属性。在这里使用三个分量定义一个材质颜色：环境光照，漫反射光照和镜面光照，通过为每个分量指定一个颜色，我们能够对物体的颜色输出有精细的控制了。

```glsl
#version 330 core
    struct Material{
        vec3 ambient;
        vec3 diffuse;
        vec3 specular;
        float shininess;
    };
uniform Material material;
```

在片段着色器中，我们创建了一个结构体来存储物体的材质，我们也可以使用独立的 `uniform` 存储，但是作为一个结构体来存储会更有条理性一些。首先定义结构体的布局，然后使用刚创建的结构体，简单的声明一个 `uniform` 变量。

可以看到，这里为每个冯氏光照模型的分量都定义了一个颜色向量。 `ambient` 材质定义了在环境光照下这个物体反射的颜色，通常这是和物体颜色相同的颜色。 `diffuse` 材质定义了在漫反射光照下物体的颜色。漫反射颜色也要设置为我们需要的物体颜色。 `specular` 材质向量定义了镜面光照对物体的颜色影响，最后 `shininess` 影响镜面高光的散射/半径。

#### 1. 设置材质

在片段着色器中使用材质的参数重新计算光照：

```glsl
void main()
{
    // 环境光
    vec3 ambient = lightColor * material.ambient;
    
    // 漫反射
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(lightPos - FragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = lightColor * (diff * material.diffuse);
    
    // 镜面光
    vec3 viewDir = normalize(viewPos - FragPos);
    vec3 reflectDir = reflect(-lightDir, norm);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = lightColor * (spec * material.specular);
    
    vec3 result = ambient + diffuse + specular;
    FragColor = vec4(result, 1.0);
}
```

可以看到，现在在需要的复方访问了材质结构体的所有属性，并且这次是根据材质的颜色来计算最终的颜色，物体的每个材质属性都乘上了他们对应的光照分量。

现在只需要在程序中设置适当的 `uniform`，对物体设置材质即可。

```cpp
shader.setVec3("material.ambient", 1.0f, 0.5f, 0.31f);
shader.setVec3("material.diffuse", 1.0f, 0.5f, 0.31f);
shader.setVec3("material.specular", 0.5f, 0.5f, 0.5f);
shader.setFloat("material.shininess", 32.0f);
```

#### 2. 光的属性

上述完成后，物体亮度太大。原因是环境光、漫反射和镜面光这三个颜色对任何一个光源都会全力反射。光源对环境光、漫反射和镜面光分量也具有不同的强度。在前面的教程，我们通过使用一个强度值改变环境光和镜面光的方式解决这个问题。这里对光照分量也同样处理：

```glsl
struct Light{
    vec3 position;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};
uniform Light light;
```

这样一个光源对它的 `ambient`、`diffuse`、`specular` 光照有着不同的强度。环境光照通常会设置一个比较低的强度，因为我们不希望环境光颜色太过显眼。光源的漫反射分量通常设置为光所具有的颜色，通常是一个比较亮的白色，镜面光分量会保持未 `vec3(1.0)` ，以最大强度发光。

这样调整了光照对物体材质的影响，我们应该能得到一个更类似于上一节的视觉效果。但是这次我们对光照和物体的材质有完全掌控。

#### 3. 不同的光源颜色

通过 `sin` 和 `glfwGetTime` 方法可以改变光源的环境光和漫反射颜色，从而可以很容易的让光源的颜色随时间变化。

```cpp
glm::vec3 lightColor;
lightColor.x = sin(glfwGetTime() * 2.0f);
lightColor.y = sin(glfwGetTime() * 0.7f);
lightColor.z = sin(glfwGetTime() * 1.3f);
glm::vec3 diffuseColor = lightColor * glm::vec3(0.5f);
glm::vec3 ambientColor = diffuseColor * glm::vec3(0.2f);
shader.setVec3("light.ambient", ambientColor);
shader.setVec3("light.diffuse", diffuseColor);
```

